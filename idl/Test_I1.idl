// Test_I1.idl File from ORB test set.
module Test {

typedef sequence <long> SeqLong;

typedef SeqLong MyAlias;

exception MyException
{
	string param;
	boolean bparam;
};

interface I1;

struct MyStruct
{
	wstring ws_member;
	long l_member;
	I1 itf;
};

enum MyEnum
{
	a, b, c
};

typedef string <8> ShortString;
typedef sequence <long, 4> ShortSeqLong;
typedef sequence <string> SeqString;
typedef sequence <MyStruct> SeqStruct;

interface I1
{
	long op1 (in long p1);
	void throw_no_implement ();
	void throw_user () raises (MyException);
	I1 object_op (in I1 in_obj, out I1 out_obj, inout I1 inout_obj);
	string string_op (in string in_s, out string out_s, inout string inout_s);
	SeqLong seq_op (in SeqLong in_s, out SeqLong out_s, inout SeqLong inout_s);
	any any_op (in any in_any, out any out_any, inout any inout_any);
	attribute long addendum;
	ShortString short_string_op (in ShortString in_s, out ShortString out_s, inout ShortString inout_s);
	ShortSeqLong short_seq_op (in ShortSeqLong in_s, out ShortSeqLong out_s, inout ShortSeqLong inout_s);
	SeqString seq_string_op (in SeqString in_s, out SeqString out_s, inout SeqString inout_s);
	MyStruct struct_op (in MyStruct in_s, out MyStruct out_s, inout MyStruct inout_s);
	SeqStruct seq_struct_op (in SeqStruct in_s, out SeqStruct out_s, inout SeqStruct inout_s);
};

};
